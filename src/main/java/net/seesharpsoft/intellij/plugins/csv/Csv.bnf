{
  parserClass="net.seesharpsoft.intellij.plugins.csv.parser.CsvParser"

  version = "3"
  extends="consulo.language.impl.psi.ASTWrapperPsiElement"

  psiClassPrefix="Csv"
  psiImplClassSuffix="Impl"
  psiPackage="net.seesharpsoft.intellij.plugins.csv.psi"
  psiImplPackage="net.seesharpsoft.intellij.plugins.csv.psi.impl"

  elementTypeHolderClass="net.seesharpsoft.intellij.plugins.csv.psi.CsvTypes"
  elementTypeClass="net.seesharpsoft.intellij.plugins.csv.psi.CsvElementType"
  tokenTypeClass="net.seesharpsoft.intellij.plugins.csv.psi.CsvTokenType"

  tokens=[
    TEXT='regexp:[^ ,:;|\t\r\n"\\]+'
    ESCAPED_TEXT='regexp:[,:;|\t\r\n]|""|\\"'
    ESCAPE_CHARACTER='regexp:\\'
    COMMA='regexp:[,:;|\t]'
    QUOTE='regexp:"'
    CRLF='regexp:\n'
    COMMENT='regexp:#.*(\n|$)'
   ]
}

csvFile ::= record (CRLF record)* [CRLF]

record ::= (COMMENT | (field (COMMA field)*))

field ::= (escaped | nonEscaped)

private escaped ::= QUOTE (TEXT | ESCAPE_CHARACTER | ESCAPED_TEXT)* QUOTE

private nonEscaped ::= (TEXT | ESCAPE_CHARACTER)*
